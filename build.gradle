buildscript {
  repositories {
    jcenter()
  }
}

allprojects {
  buildscript {
    repositories {
      jcenter()
      mavenCentral()
    }
  }
  repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
  }
}

def buildSrcDir = 'buildsystem'
apply from: "${buildSrcDir}/inject.gradle"

task wrapper(type: Wrapper) {
  gradleVersion = project.gradleVersion
}

task cleanFrodo(type: Exec) {
  description = 'Cleans all Frodo projects.'
  commandLine './gradlew', 'clean'
}

task installFrodoApi(type: Exec) {
  description = 'Assembles and installs Frodo Api in the local maven repository.'
  commandLine './gradlew', '-p', 'frodo2-api/', 'assemble', 'install'
}

task installFrodoRuntime(type: Exec) {
  description = 'Assembles and installs Frodo Core Runtime Library in the local maven repository.'
  commandLine "./gradlew", "-p", "frodo2-runtime/", "assembleDebug", "install", "-x", "lint"
}

task installFrodoPlugin(type: Exec) {
  description = 'Assembles and installs Frodo Gradle Plugin in the local maven repository.'
  commandLine "./gradlew", "-p", "frodo2-plugin/", "build", "install"
}

task installFrodoAndroidSample(type: Exec,
    dependsOn: ['installFrodoApi', 'installFrodoRuntime', 'installFrodoPlugin']) {
  description = 'Assembles and installs Frodo Example Android application.'
  commandLine "./gradlew", "clean", "-p", "frodo2-android-sample/", "assembleDebug", "installDebug"
}
installFrodoAndroidSample.mustRunAfter('installFrodoApi', 'installFrodoRuntime',
    'installFrodoPlugin')

task runUnitTests(
    dependsOn: [':frodo2-runtime:cleanTestDebugUnitTest', ':frodo2-runtime:testDebugUnitTest']) {
  description = 'Run unit tests for frodo runtime.'
}
